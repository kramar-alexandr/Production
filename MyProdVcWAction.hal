remote updating procedure InsertProdOperationRow(var record MyProdVc,integer);
remote function boolean TestNextProdOperationExists(record MyProdVc,integer);
external inner procedure ExtractObj(string,var Integer,var string);
external inner procedure INTransferVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure OffHireVc_SerialNrSClass(Integer,Integer,var Array string,var Integer);
external inner procedure InternMovVc_SerialNrSClass(Integer,Integer,var Array string,var Integer);
external inner procedure CollectionVc_SerialNrSClass(Integer,Integer);
external inner procedure DispatchVc_SerialNrSClass(Integer,Integer);
external inner procedure RentINVc_SerialNrSClass(Integer,Integer);
external inner procedure RentResVc_SerialNrSClass(Integer,Integer);
external inner procedure QualConVc_SerialNrSClass(Integer,Integer,var Array string,var Integer);
external inner procedure RetVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure WSVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure COVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure StockTakeVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure StockMovVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure DropSHVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure SHVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure SDVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure SRVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure PUVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure ProdVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure ORVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure IVCashVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure BatchTextVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure IVVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure BPCVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure RetPUVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure ProdOperationVc_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure SerialNrRClass_SerialNrSClass(string,Integer,Integer,Integer,var Array string,var Integer);
external inner procedure SerialNoScanNPTSSClass_SerialNrSClass(Integer,Integer,Integer,var Array string,var Integer);
external inner procedure FillSerialNr(string,string,var Array string,var Integer);
external inner procedure FillSerialNr2(string,string,var Array string,var Integer);

SetLangMode(LangRussian,"RUS",0);

function boolean MyProdDClassMotherOperationsEFAfter(integer wn,integer rownr,integer changed)
begin
	boolean res;
	integer mtrw,i,pos;
	string 20 tstr;
	record MyProdVc MyProdr;
	row MyProdVc MyProdrw;
	boolean foundf;
	string 200 prodstr;
	
	res = true;
	if(changed>0)then begin
		getwindowrecord(wn,MyProdr);
		mtrw = matrowcnt(MyProdr);
		matrowget(MyProdr,rownr,MyProdrw);
		prodstr = MyProdrw.MotherOperations;
		if(nonblank(prodstr))then begin
			pos = 0;
			ExtractObj(prodstr,pos,tstr);
			While (nonblank(tstr)) begin
				foundf = false;
				if(nonblank(tstr))then begin
					For(i=0;i<rownr;i=i+1) begin
						matrowget(MyProdr,i,MyProdrw);
						if(MyProdrw.stp==2 and MyProdrw.OperationNo==stringtoint(tstr))then begin
							foundf = true;
						end;
					end;
					if(foundf==false)then begin
						res = false;
						messagebox(0,"Operation not found");
						goto LProdDClassMotherOperationsEFAfter;
					end;
				end;
				ExtractObj(prodstr,pos,tstr);
			end;
			
			 
		end;
	end;
	
	MyProdDClassMotherOperationsEFAfter = res;
	LProdDClassMotherOperationsEFAfter:;
	
return;
end;

function boolean MyProdDClassItemEFAfter(integer wn,integer rownr,integer changed)
begin
  record MyProdVc MyProdr;
  record INVc INr;
  row MyProdVc MyProdrw;	
  Integer i,rwcnt;
  Boolean res,foundf;

  res = true;
  if (changed!=0) then begin
    GetWindowRecord(wn,MyProdr);
    MatRowGet(MyProdr,rownr,MyProdrw);
    if (rownr==0) then begin
      MessageBox(0,"Добавьте операцию");
      MatRowGet(MyProdr,rownr,MyProdrw);
      MatRowDelete(MyProdr,rownr);
      res = false;     
    end else begin
      rwcnt = MatRowCnt(MyProdr);
      if (rownr==rwcnt-1) then begin
        foundf = false;
        MatRowGet(MyProdr,rownr,MyProdrw);
        if (MyProdrw.stp==1) then begin
          for (i=rownr;i>=0;i=i-1) begin
            if ((res) and (foundf==false)) then begin
              MatRowGet(MyProdr,i,MyProdrw);
              if (MyProdrw.stp==2) then begin
                foundf = true;
              if (MyProdrw.Ordered1!=0) then begin 
                  MessageBox(0,"Данная операция уже записана. Добавьте новую");
                  MatRowGet(MyProdr,rownr,MyProdrw);
                  if Blank(MyProdrw.Comment) then begin
                    MatRowGet(MyProdr,rownr,MyProdrw);
                    MatRowDelete(MyProdr,rownr);
                  end else begin
                    INr.Name = MyProdrw.Comment;
                    ReadFirstKey("Name",INr,1,true);
                    MyProdrw.Item = INr.Code;
                    MatRowPut(MyProdr,rownr,MyProdrw);
                  end;
                  res = false;
                end;
              end;
            end;
          end;
        end;
      end;
      if (res) then begin
        MatRowGet(MyProdr,rownr,MyProdrw);
        INr.Code=MyProdrw.Item;
        if (ReadFirstMain(INr,1,true)) then begin
          MyProdrw.Comment = INr.Name;
          MatRowPut(MyProdr,rownr,MyProdrw);
          res = true;
        end else begin
          MessageBox(0,"Код товара не зарегистрирован");
          if Blank(MyProdrw.Comment) then begin
            MatRowGet(MyProdr,rownr,MyProdrw);
            MatRowDelete(MyProdr,rownr);
          end else begin
            INr.Name = MyProdrw.Comment;
            ReadFirstKey("Name",INr,1,true);
            MyProdrw.Item = INr.Code;
            MatRowPut(MyProdr,rownr,MyProdrw);
          end;
          res = false;     
        end;
      end;
    end;
    PutWindowRecord(wn,MyProdr);
  end;
  MyProdDClassItemEFAfter = res;
  return;
end;

function boolean MyProdDClassOutQtyEFAfter(integer wn,integer rownr,integer changed)
begin
  record MyProdVc MyProdr;
  record INVc INr;
  row MyProdVc MyProdrw;	
  Integer i;
  Boolean res;

  res = true;
  if (changed!=0) then begin
    GetWindowRecord(wn,MyProdr);
    MatRowGet(MyProdr,rownr,MyProdrw);
    INr.Code=MyProdrw.Item;
    if (ReadFirstMain(INr,1,true)) then begin
      if ((INr.SerNrf!=0) and NonBlank(MyProdrw.OutQty) and Blank(MyProdrw.SerialNr)) then begin
        MessageBox(0,"Введите серийный номер товара");
        res = false;     
      end;
    end;
  end;
  MyProdDClassOutQtyEFAfter = res;
  return;
end;

function boolean MyProdDClassInQtyEFAfter(integer wn,integer rownr,integer changed)
begin
  record MyProdVc MyProdr;
  record INVc INr;
  row MyProdVc MyProdrw;	
  Integer i;
  Boolean res;

  res = true;
  if (changed!=0) then begin
    GetWindowRecord(wn,MyProdr);
    MatRowGet(MyProdr,rownr,MyProdrw);
    INr.Code=MyProdrw.Item;
    if (ReadFirstMain(INr,1,true)) then begin
      if ((INr.SerNrf!=0) and NonBlank(MyProdrw.InQty) and Blank(MyProdrw.SerialNr)) then begin
        MessageBox(0,"Введите серийный номер товара");
        res = false;     
      end;
    end;
  end;
  MyProdDClassInQtyEFAfter = res;
  return;
end;

global
function Boolean MyProdDClassAfterEditField(Integer wn,string fieldname,Integer fn,Integer rownr,Integer changed)
BEGIN
  Boolean res;

	res = inner.MyProdDClassAfterEditField(wn,fieldname,fn,rownr,changed);

  switch (fieldname) begin
    case "MotherOperations": res = MyProdDClassMotherOperationsEFAfter(wn,rownr,changed);
    case "Item":  res = MyProdDClassItemEFAfter(wn,rownr,changed);
    case "OutQty":  res = MyProdDClassOutQtyEFAfter(wn,rownr,changed);
    case "InQty":  res = MyProdDClassInQtyEFAfter(wn,rownr,changed);
  end;
  MyProdDClassAfterEditField = res;
  RETURN;
END;

global 
function Boolean MyProdDClassInsertRowTest(Integer wn, Integer rownr)
begin
  Boolean res,foundf;
  record MyProdVc MyProdr;
  record ProdOperationVc ProdOpr;
	row MyProdVc MyProdrw;
	integer i;

  res = true;
  switch (WindowState(wn)) begin
    case Rs_normal:
      GetWindowRecord(wn,MyProdr);
      if (MyProdr.PRStatusFlag>=3) and (MyProdr.PRStatusFlag<=5) then begin res = false; end;
      if (MyProdr.Disassemble!=0) then begin res = false; end;
    case Rs_insert:
      res = true;
    case Rs_update:
      GetPrevWindowRecord(wn,MyProdr);
      if (MyProdr.PRStatusFlag>=3) and (MyProdr.PRStatusFlag<=5) then begin res = false; end;
      if (MyProdr.Disassemble!=0) then begin res = false; end;
    otherwise
      res = false;
  end;
  
  if(res)then begin
    foundf = false;
    MatRowGet(MyProdr,rownr,MyProdrw);
    switch (MyProdrw.stp) begin
      case 1:
        if NonBlank(MyProdrw.Item) then begin
          for (i=rownr;i>=0;i=i-1) begin
            if ((res) and (foundf==false)) then begin
              MatRowGet(MyProdr,i,MyProdrw);
              if (MyProdrw.stp==2) then begin
                foundf = true;
                if (MyProdrw.Ordered1!=0) then begin 
                  MessageBox(0,"Данная операция уже записана. Добавьте новую");
                  res = false;
                end;
              end;
            end;
          end;
        end;
      case 2:
        if (MyProdrw.Ordered1!=0) then begin 
          MessageBox(0,"Данная операция уже записана. Добавьте новую");
          res = false;
        end;
      case 3:
        for (i=rownr;i>=0;i=i-1) begin
          if ((res) and (foundf==false)) then begin
            MatRowGet(MyProdr,i,MyProdrw);
            if (MyProdrw.stp==2) then begin
              foundf = true;
              if (MyProdrw.Ordered1!=0) then begin 
                MessageBox(0,"Данная операция уже записана. Добавьте новую");
                res = false;
              end;
            end;
          end;
        end;
      otherwise
        res = false;
    end;
  
  	//res = TestNextProdOperationExists(MyProdr,rownr);// Edit ************************** Monday, 5 May 2014 16:19:07
  end;

  MyProdDClassInsertRowTest = res;
  return;
end;

global 
function Boolean MyProdDClassDeleteRowTest(Integer wn,Integer rownr)
begin
  record MyProdVc MyProdr;
  record ProdOperationVc ProdOpr;
  row MyProdVc MyProdrw;
  Boolean res,foundf;
  Integer i;
  
  res = true;
  switch (WindowState(wn)) begin
    case Rs_normal:
      GetWindowRecord(wn,MyProdr);
      if (MyProdr.PRStatusFlag>=3) and (MyProdr.PRStatusFlag<=5) then begin res = false; end;
      if (MyProdr.Disassemble!=0) then begin res = false; end;
    case Rs_insert:
      res = true;
    case Rs_update:
      GetPrevWindowRecord(wn,MyProdr);
      if (MyProdr.PRStatusFlag>=3) and (MyProdr.PRStatusFlag<=5) then begin res = false; end;
      if (MyProdr.Disassemble!=0) then begin res = false; end;
    otherwise
      res = false;
  end;
  if (res) then begin
    GetWindowRecord(wn,MyProdr);
    MatRowGet(MyProdr,rownr,MyProdrw);
    if (NonBlank(MyProdrw.Item) or NonBlank(MyProdrw.OperationNo)) then begin
      switch (MyProdrw.stp) begin
        case 1:
          foundf = false;
          for (i=rownr;i>=0;i=i-1) begin
            if ((res) and (foundf==false)) then begin
              MatRowGet(MyProdr,i,MyProdrw);
              if (MyProdrw.stp==2) then begin
                foundf = true;
                if (MyProdrw.Ordered1!=0) then begin 
                  MessageBox(0,"Невозможно удалить товар из сохраненной операции");
                  res = false;
                end;
              end;
            end;
          end;
        case 2:
          MessageBox(0,"Для удаления операции используйте специальное меню");
          res = false;
        case 3:
          foundf = false;
          for (i=rownr;i>=0;i=i-1) begin
            if ((res) and (foundf==false)) then begin
              MatRowGet(MyProdr,i,MyProdrw);
              if (MyProdrw.stp==2) then begin
                foundf = true;
                if (MyProdrw.Ordered1!=0) then begin 
                  MessageBox(0,"Невозможно удалить инструкцию из сохраненной операции");
                  res = false;
                end;
              end;
            end;
          end;
      end;
    end;
  	//res = TestNextProdOperationExists(MyProdr,rownr);// Edit ************************** Monday, 5 May 2014 16:19:07
  end;
  MyProdDClassDeleteRowTest = res;
  return;
end;

global
updating procedure GenSerialNrMyProd()
begin
  Integer wn, rownr, i;
  record MyProdVc MyProdr;
  row MyProdVc MyProdrw, MyProd2rw;
  
  wn = CurWindow();
  if (WindowState(wn)!=Rs_insert) then begin
    rownr = WindowActiveRow(wn);
    GetWindowRecord(wn,MyProdr);
    DeselectWindow(wn,false);
    MatRowGet(MyProdr,rownr,MyProdrw);
    if (NonBlank(MyProdrw.InQty) or NonBlank(MyProdrw.OutQty)) then begin
      if NonBlank(MyProdrw.InQty) then begin
        for (i=0;i<rownr-1;i=i+1) begin
          MatRowGet(MyProdr,i,MyProd2rw);
          if ((MyProd2rw.Item==MyProdrw.Item) and NonBlank(MyProd2rw.OutQty)) then begin
            MyProdrw.SerialNr = MyProd2rw.SerialNr;
            if NonBlank(MyProd2rw.LocationItem) then begin
              MyProdrw.LocationItem = MyProd2rw.LocationItem;
            end;
          end;
        end;
        if Blank(MyProdrw.SerialNr) then begin
          MessageBox(0, "Этот товар не был изготовлен в данном производстве");
        end;
      end else begin
        for(i=0;i<rownr;i=i+1) begin
          matrowget(MyProdr,i,MyProd2rw);
          if(MyProd2rw.stp==2)then begin
            MyProdrw.SerialNr = MyProdr.SerNr & "-" & MyProd2rw.OperationNo;
            if NonBlank(MyProd2rw.LocProdOper) then begin
              MyProdrw.LocationItem = MyProd2rw.LocProdOper;
            end else begin
              MyProdrw.LocationItem = MyProdr.Location;
            end;
          end;
        end;
      end;
      MatRowPut(MyProdr,rownr,MyProdrw);
      PutWindowRecord(wn,MyProdr);
    end else begin
      MessageBox(0, "Заполните поле 'Затр.' или 'Выпуск'");
    end;
  end else begin
    MessageBox(0, "Сохраните запись");
  end;

  return;
end;

global
updating procedure AddProdOperRowDsm()
begin
  Integer wn,rownr,mtrw,opermax,insertrowno,insertrowcnt,i;
  record MyProdVc MyProdr;
	row MyProdVc MyProdrw,MyProd2rw;
	Boolean testf;
	
	testf = false;
  wn = CurWindow;
	rownr = WindowActiveRow(wn);
	//DeselectWindow(wn,false);
	GetWindowRecord(wn,MyProdr);
	mtrw = matrowcnt(MyProdr);
	if(rownr==-1 or rownr>=mtrw+1)then begin
		insertrowcnt = mtrw - 1;
		if ((rownr==-1) or (mtrw<=1)) then begin
		  insertrowcnt = mtrw;
		end;
    if (insertrowcnt!=0) then begin
      MatRowGet(MyProdr,insertrowcnt-1,MyProd2rw);
      if ((MyProd2rw.stp==1) and Blank(MyProd2rw.Item)) then begin
        insertrowcnt = insertrowcnt - 1;
      end;
    end;
		opermax = 0;
		For(i=0;i<mtrw;i=i+1) begin
			matrowget(MyProdr,i,MyProdrw);
			if(MyProdrw.stp==2)then begin
				if(MyProdrw.OperationNo>opermax)then begin
					opermax = MyProdrw.OperationNo;
				end;
			end;
		end;
		insertrowno = opermax + 1;
		clearrow(MyProdr,MyProdrw,2);
		MyProdrw.OperationNo = insertrowno;
		matrowinsert(MyProdr,insertrowcnt,MyProdrw);
	end else begin
		
		InsertProdOperationRow(MyProdr,rownr);
		
	end;
	putwindowrecord(wn,MyProdr);

  return;
end;

global
updating procedure AddProdInstructionRowDsm()
begin
  Integer wn,nwn;
  Integer rownr,err,mtrw,opermin,opermax,operminrow,opermaxrow,insertrowno,insertrowcnt;
  record MyProdVc MyProdr;
	row MyProdVc MyProdrw;
	integer i;
	boolean res,foundf;
	
  wn = CurWindow;
	rownr = WindowActiveRow(wn);
	//DeselectWindow(wn,false);
	GetWindowRecord(wn,MyProdr);
	mtrw = matrowcnt(MyProdr);
	if(rownr==-1 or rownr>=mtrw-1)then begin
		insertrowcnt = mtrw - 1;
		if ((rownr==-1) or (mtrw<=1)) then begin
		  insertrowcnt = mtrw;
		end;
    if (insertrowcnt!=0) then begin
      MatRowGet(MyProdr,insertrowcnt-1,MyProdrw);
      if ((MyProdrw.stp==1) and Blank(MyProdrw.Item)) then begin
        insertrowcnt = insertrowcnt - 1;
      end;
    end;
	end else begin
	  insertrowcnt = rownr;
	end;
  foundf = false;
  res = true;
  MatRowGet(MyProdr,insertrowcnt-1,MyProdrw);
  if (MyProdrw.stp==2) then begin
    if (MyProdrw.Ordered1!=0) then begin 
      MessageBox(0,"Данная операция уже записана");
      res = false;
    end;
  end;
  if ((MyProdrw.stp==1) or (MyProdrw.stp==3) or (MyProdrw.stp==0)) then begin
    for (i=insertrowcnt-1;i>=0;i=i-1) begin
      if ((res) and (foundf==false)) then begin
        MatRowGet(MyProdr,i,MyProdrw);
        if (MyProdrw.stp==2) then begin
          foundf = true;
          if (MyProdrw.Ordered1!=0) then begin 
            MessageBox(0,"Данная операция уже записана");
            res = false;
          end;
        end;
      end;
    end;
  end;

  if (res) then begin
    clearrow(MyProdr,MyProdrw,3);
    matrowinsert(MyProdr,insertrowcnt,MyProdrw);
    putwindowrecord(wn,MyProdr);
  end;
  return;
end;


global
updating procedure DeleteProdOperRowDsm()
begin
  record MyProdVc MyProdr;
  Boolean testf,foundf;
  row MyProdVc MyProdrw,MyProd2rw;
  Integer wn,rownr,i,mtrw,pos;
	string 200 prodstr;
	string 20 tstr;
  
  testf = true;
  foundf = false;
  pos = 0;
  wn = CurWindow;
	rownr = WindowActiveRow(wn);
	GetWindowRecord(wn,MyProdr);
  MatRowGet(MyProdr,rownr,MyProdrw);
  if (MyProdrw.stp==2) then begin
    if (MyProdrw.Ordered1==0) then begin 
      mtrw = matrowcnt(MyProdr);
      for (i=rownr;i<mtrw;i=i+1) begin
        MatRowGet(MyProdr,i,MyProd2rw);
        if (MyProd2rw.stp==2) then begin
          prodstr = MyProd2rw.MotherOperations;
          if nonblank(prodstr) then begin
            ExtractObj(prodstr,pos,tstr);
            while (nonblank(tstr)) begin
              if (MyProdrw.OperationNo==stringtoint(tstr)) then begin
                foundf = true;
              end;
              ExtractObj(prodstr,pos,tstr);
            end;
          end;
        end;
      end;
      if (foundf==false) then begin
        MatRowDelete(MyProdr,rownr);
        while (testf) begin
          mtrw = MatRowCnt(MyProdr);
          if (rownr<mtrw) then begin
            MatRowGet(MyProdr,rownr,MyProd2rw);
            if (MyProd2rw.stp!=2) then begin
              MatRowDelete(MyProdr,rownr);
              testf = true;
            end else begin
              testf = false;
            end;
          end else begin
            testf = false;
          end;
        end;
        putwindowrecord(wn,MyProdr);
      end else begin
        MessageBox(0,"Операция является исходной для других операций");
      end;
    end else begin
      MessageBox(0,"Операция уже сохранена");
    end;
  end else begin
    MessageBox(0,"Выберите строку с операцией");
  end;

end;

global
procedure MyProdVc_SerialNrSClass(Integer mwn,var Array string asernrstr,var Integer acnt)
BEGIN
  record MyProdVc MyProdr;
  record SerBalVc SerBalr;
  row MyProdVc MyProdrw,MyProd2rw;
  Integer rn,rwcnt,i;
  Boolean foundf;
  string 10 location; 
  
  foundf = false;
  acnt = 1;
  GetWindowRecord(mwn,MyProdr);
  rn = WindowActiveRow(mwn);
  rwcnt = MatRowCnt(MyProdr);
  MatRowGet(MyProdr,rn,MyProdrw);
  if NonBlank(MyProdrw.Item) then begin
    if NonBlank(MyProdrw.InQty) then begin
      for (i=0;i<rn-1;i=i+1) begin
        MatRowGet(MyProdr,i,MyProd2rw);
        if ((MyProd2rw.Item==MyProdrw.Item) and NonBlank(MyProd2rw.OutQty)) then begin
          asernrstr[acnt] = MyProd2rw.SerialNr;
        end;
      end;
      if NonBlank(asernrstr[acnt]) then begin
        asernrstr[acnt] = asernrstr[acnt] & "                                  "
                          & "Изготовлено в опер. № " & Right(asernrstr[acnt],1);
        acnt = acnt + 1;
      end;
    end;
    if NonBlank(MyProdrw.OutQty) then begin
      for(i=0;i<rn;i=i+1) begin
        matrowget(MyProdr,i,MyProd2rw);
        if (MyProd2rw.stp==2) then begin
          asernrstr[acnt] = MyProdr.SerNr & "-" & MyProd2rw.OperationNo;
        end;
      end;
      if NonBlank(asernrstr[acnt]) then begin
        asernrstr[acnt] = asernrstr[acnt] & "                                  "
                          & "Сгенерированый сер. ном.";
        acnt = acnt + 1;
      end;
    end;
    if NonBlank(MyProdrw.LocationItem) then begin
      location = MyProdrw.LocationItem;
    end;
    if Blank(location) then begin
      for (i=rn;i>=0;i=i-1) begin
        MatRowGet(MyProdr,i,MyProd2rw);
        if ((MyProd2rw.stp==2) and (foundf==false)) then begin
          foundf = true;
          if NonBlank(MyProd2rw.LocProdOper) then begin
            location = MyProd2rw.LocProdOper;
          end;
        end;
      end;
    end;
    if Blank(location) then begin
      if NonBlank(MyProdr.Location) then begin
        location = MyProdr.Location;
      end;
    end;
    if NonBlank(location) then begin
      MyProdrw.LocationItem = location;
      MatRowPut(MyProdr,rn,MyProdrw);
      PutWindowRecord(mwn,MyProdr);
      if (acnt<=1) then begin
        FillSerialNr(MyProdrw.Item,location,asernrstr,acnt);
      end;
    end else begin
      MessageBox(0,"Информация о складе отсутствует");
    end;
  end else begin
    MessageBox(0,"Выберите товар");
  end;

  RETURN;
END;

global
function Boolean SerialNrSClassOnOpenWindow(Integer wn)
begin
  Integer mwn;
  Array string 100 asernrstr;
  Integer acnt,i;
  
  mwn = MotherWindow(wn);
  switch (GetWindowFileName(mwn)) begin
    case "BPCVc": BPCVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "BatchTextVc": BatchTextVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "IVVc": IVVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "IVCashVc": IVCashVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "ORVc": ORVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "ProdVc": ProdVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "PUVc": PUVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "RetVc": RetVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "SDVc": SDVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "SRVc": SRVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "SHVc": SHVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "DropSHVc": DropSHVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "StockMovVc": StockMovVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "StockTakeVc": StockTakeVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "WSVc": WSVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "COVc": COVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "OffHireVc": OffHireVc_SerialNrSClass(wn,mwn,asernrstr,acnt);
    case "INTransferVc": INTransferVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "InternMovVc": InternMovVc_SerialNrSClass(wn,mwn,asernrstr,acnt);
    case "CollectionVc": CollectionVc_SerialNrSClass(wn,mwn);
    case "RentResVc": RentResVc_SerialNrSClass(wn,mwn);
    case "QualConVc": QualConVc_SerialNrSClass(wn,mwn,asernrstr,acnt);
    case "DispatchVc": DispatchVc_SerialNrSClass(wn,mwn);
    case "RentINVc": RentINVc_SerialNrSClass(wn,mwn);
    case "RetPUVc": RetPUVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "ProdOperationVc": ProdOperationVc_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
    case "MyProdVc": MyProdVc_SerialNrSClass(mwn,asernrstr,acnt);
    otherwise
      switch (GetWindowClass(mwn)) begin
        case "SerialNrRClass": SerialNrRClass_SerialNrSClass("",1,wn,mwn,asernrstr,acnt);
        case "INPosRClass": SerialNrRClass_SerialNrSClass("",1,wn,mwn,asernrstr,acnt);
        case "PULabRClass": SerialNrRClass_SerialNrSClass("f3",1,wn,mwn,asernrstr,acnt);
        case "QualConRClass": SerialNrRClass_SerialNrSClass("",1,wn,mwn,asernrstr,acnt);
        case "QualConResRClass": SerialNrRClass_SerialNrSClass("",1,wn,mwn,asernrstr,acnt);
        case "SerialNoScanNPTSSClass": SerialNoScanNPTSSClass_SerialNrSClass(1,wn,mwn,asernrstr,acnt);
        otherwise ;
      end;  
  end;
  for (i=0;i<acnt;i=i+1) begin
    SetListString(wn,len(asernrstr[i]),asernrstr[i],false);
  end;      
  SerialNrSClassOnOpenWindow = false;
  return;
end;